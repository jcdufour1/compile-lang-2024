type io import = std.io
type rt import = std.runtime
type libc import = std.libc
type coll import = std.collections

type Optional enum(<ItemT>) {
    some ItemT
    none
}

type Result enum(<OkT, ErrorT>) {
    ok OkT
    error ErrorT
}

type usize def = rt.usize

// overlapping regions are allowed
// count_items is the number of items to move, not the number of bytes
fn move_items(<ItemT>)(dest ItemT*, src ItemT*, count_items u64) {
    libc.memmove(unsafe_cast<opaque*>dest, unsafe_cast<opaque*>src, sizeof(*dest)*count_items);
}

// TODO: (return ItemT instead of ItemT*)? (because ptr could be used as alias for opaque* in the future)
fn null(<ItemT>)() ItemT* {
    return unsafe_cast<ItemT*>0
}

fn bitcast(<CastTo, From>)(item From) CastTo {
    return *unsafe_cast<CastTo*>&item
}

fn swap(<ItemT>)(lhs ItemT*, rhs ItemT*) {
    let temp ItemT = *lhs
    *lhs = *rhs
    *rhs = temp
}

// TODO: return optional, etc.?
fn alloc(<ItemT>)(count_items u64, file Str = #file, line i32 = #line, column i32 = #column) ItemT* {
    let dummy ItemT* = null(<ItemT>)();
    let buf ItemT* = unsafe_cast<ItemT*>libc.malloc(sizeof(*dummy)*count_items)
    assert(buf != null(<ItemT>)(), "out of memory", .file = file, .line = line, .column = column)
    return buf
}

// TODO: return optional, etc.?
fn realloc(<ItemT>)(buf ItemT*, new_cnt_items u64, file Str = #file, line i32 = #line, column i32 = #column) ItemT* {
    buf = unsafe_cast<ItemT*>libc.realloc(unsafe_cast<opaque*>buf, sizeof(ItemT)*new_cnt_items)
    assert(buf != null(<ItemT>)(), "out of memory", .file = file, .line = line, .column = column)
    return buf
}

fn free(<ItemT>)(buf ItemT*) {
    libc.free(unsafe_cast<opaque*>buf)
}

// TODO: remove keep_going parameter from std?
fn assert(condition u1, message Str = "", keep_going u1 = 0, file Str = #file, line i32 = #line, column i32 = #column) {
    if (!condition) {
        io.own_fprintf(
            io.stderr(),
            "%.*s:%d:%d:assertion failure: %s\n",
            unsafe_cast<i32>file.count,
            file.buf,
            unsafe_cast<i32>line,
            unsafe_cast<i32>column,
            message
        )
        if (!keep_going) {
            libc.abort()
        }
    }
}

// NOTE: cstr_free should be called on the returned string later
fn str_to_cstr(str coll.Str) u8* {
    let cstr coll.Darr(<u8>) = coll.darr_new(<u8>)(str)
    coll.darr_append(&cstr, '\0')
    return cstr.buf
}

fn cstr_free(cstr u8*) {
    libc.free(unsafe_cast<opaque*>cstr)
}

fn slice_from_cstr(cstr u8*) Str {
    return {cstr, strlen(cstr)}
}
